plugins {
    id 'java'
    id 'antlr'
    id 'groovy-gradle-plugin'
}

repositories {
    mavenCentral()
}

dependencies {
    antlr 'org.antlr:antlr4:4.13.1'
    implementation 'org.antlr:antlr4-runtime:4.13.1'
}

generateGrammarSource  {
    source = fileTree("src/main/antlr") {
        include("wresl.g4")
    }
    arguments += ['-visitor', '-long-messages']
    outputDirectory = file("$buildDir/generated-src/antlr")
}

sourceSets {
    main {
        java.srcDir "$buildDir/generated-src/antlr"
        antlr.srcDir "src/main/antlr"
    }
}

tasks.register('runWreslParser', JavaExec) {
    group = 'antlr'
    description = 'Parse a WRESL file using the generated ANTLR parser.'
    dependsOn tasks.generateGrammarSource
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'org.antlr.v4.gui.TestRig'

    doFirst {
        if (!project.hasProperty('file')) {
            throw new GradleException("Usage: gradle runWreslParser -Pfile=path/to/input.wresl")
        }
    }

    args = ['wresl', 'start', '-tokens', '-gui', project.property('file')]
}
